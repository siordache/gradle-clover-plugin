import com.bmuschko.gradle.clover.CloverPlugin

buildscript {
    dependencies {
        classpath files('../../../../build/classes/java/main', '../../../../build/classes/groovy/main')
    }
}

apply from: '../deps.gradle'
apply plugin: 'java'
apply plugin: CloverPlugin

repositories {
    jcenter()
}

dependencies {
    testCompile deps.junit
    clover deps.clover
}

sourceSets {
    integTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integrationTest(type: Test, dependsOn: test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
}

check.dependsOn integrationTest

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(GroovyCompile) {
    groovyOptions.encoding = 'UTF-8'
    options.encoding = 'UTF-8'
}

clover {
    additionalTestSourceSet {
        srcDirs = sourceSets.integTest.allSource.srcDirs
        classesDir = sourceSets.integTest.output.classesDir
    }

    compiler {
        encoding = 'UTF-8'
    }

    report {
        json = true
        html = true
        pdf = true
    }
}
